dnl Process this file with autoconf to produce a configure script.
AC_INIT()

AC_PROG_INSTALL
AC_SUBST(INSTALL)

dnl
dnl Fonts installation directory.
dnl
AC_ARG_WITH(fontdir,
    [  --with-fontdir=DIR\t  install fonts in DIR [PREFIX/share/emacs/fonts]])
fontdir=${with_fontdir}
if test -z "$fontdir" -o "${fontdir}" = yes; then
  theprefix=$prefix
  if test "x$theprefix" = "xNONE"; then
    theprefix=$ac_default_prefix
  fi
  fontdir="${theprefix}/share/emacs/fonts"
fi
AC_MSG_CHECKING([where fonts should go])
AC_MSG_RESULT(${fontdir})
AC_SUBST(fontdir)

dnl
dnl Compress fonts nor not
dnl
AC_ARG_ENABLE(compress,
  [  --enable-compress[=ARG] whether to compress PCF fonts or not [ARG=gzip]])
if test -z "${enable_compress}"; then
  compress=no
elif test "${enable_compress}" = "yes"; then
  compress=gzip
else
  compress=${enable_compress}
fi
AC_MSG_CHECKING(whether to compress fonts or not)
if test "${compress}" = "no" ; then
  AC_MSG_RESULT(no)
else
  AC_MSG_RESULT([yes, by ${compress}])
fi
AC_SUBST(compress)

dnl
dnl Installing font types
dnl
AC_ARG_WITH(pcf,
	[  --with-pcf[=ARG]	  install PCF fonts
			  [ARG=yes] on Unix, [ARG=no] on DOS],,
	test -z "${COMSPEC}" || with_pcf=no)
AC_ARG_WITH(bdf,
	[  --with-bdf[=ARG]	  install BDF fonts [ARG=yes]])
AC_ARG_WITH(type1,
	[  --with-type1[=ARG]	  install Type1 fonts [ARG=no]],,
	with_type1=no)
AC_ARG_WITH(truetype,
	[  --with-truetype[=ARG]	  install TrueType fonts [ARG=no]],,
	with_truetype=no)
targets=""
test "${with_pcf}" = "no" || targets="${targets} pcf"
test "${with_bdf}" = "no" || targets="${targets} bdf"
test "${with_type1}" = "no" || targets="${targets} type1"
test "${with_truetype}" = "no" || targets="${targets} truetype"
AC_MSG_CHECKING(installing font types)
AC_MSG_RESULT($targets)
AC_SUBST(targets)

AC_OUTPUT(Makefile)
